plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    // Main dependencies for test extensions and utilities
    implementation 'org.junit.jupiter:junit-jupiter-api:5.11.2'
    implementation 'org.junit.jupiter:junit-jupiter-params:5.11.2'
    
    // Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            // Set the correct artifact name to maintain compatibility
            artifactId = 'optivem-test'
            from components.java
            
            pom {
                name = 'Optivem Test Library'
                description = 'A simple calculator library for testing purposes'
                url = 'https://github.com/optivem/optivem-test-java'
                
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                
                developers {
                    developer {
                        id = 'optivem'
                        name = 'Optivem'
                        email = 'info@optivem.com'
                    }
                }
                
                scm {
                    connection = 'scm:git:git://github.com/optivem/optivem-test-java.git'
                    developerConnection = 'scm:git:ssh://github.com:optivem/optivem-test-java.git'
                    url = 'https://github.com/optivem/optivem-test-java'
                }
            }
        }
    }
    
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/optivem/optivem-test-java")
            credentials {
                username = System.getenv("GITHUB_USERNAME")
                password = System.getenv("GITHUB_WRITE_PACKAGES_TOKEN")
            }
        }
    }
}

signing {
    required = { gradle.taskGraph.hasTask("publish") }
    sign publishing.publications.maven
}